allprojects {
	group='fi.jasoft'
	version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : 'development';
}

subprojects {
	apply from: 'http://plugins.jasoft.fi/vaadin.plugin'
	apply plugin: 'eclipse-wtp'
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
}


/**
 * Vaadin addon project
 */
project(':addon'){
    apply plugin: 'maven'
    apply plugin: 'osgi'

    dependencies{
        testCompile     group:'junit',  name:'junit',   version: '4.8.+'
    }

    vaadin {
        widgetset = 'fi.jasoft.dragdroplayouts.DragDropLayoutsWidgetSet'

				gwt {
					userAgent = 'ie8,ie9,gecko1_8,safari,ie10'
				}

        addon {
            author 'John Ahlroos'
            license 'Apache 2.0'
            title 'DragDropLayouts'
        }
    }

    jar {
        baseName = 'dragdroplayouts'
        dependsOn test

        sourceSets.main.java.srcDirs.each{
            from it
        }
        manifest { // the manifest of the default jar is of type OsgiManifest
            name = 'dragdroplayouts'
/*
  it seems we need to export the client-side stuff too to make it easier to include the jar file as project dependency
            instruction 'Export-Package', '!fi.jasoft.dragdroplayouts.client.*', '*'
*/
	    instruction 'Import-Package', '!com.google.gwt.*', '!com.vaadin.client.*', '*'
            instruction 'Bundle-Vendor', 'johndevs'
            instruction 'Bundle-Description', 'Drag and drop layouts addon'
            instruction 'Bundle-DocURL', 'https://github.com/johndevs/dragdroplayouts'
        }
    }

    task createAddonPom {
        pom{
            artifactId = 'dragdroplayouts'
        }.writeTo("META-INF/maven/fi.jasoft/dragdroplayouts/pom.xml")
    }
}


/*
 * Demo application for demonstrating the addon
 */
project(':demo'){
    dependencies {
        compile project(':addon')
        compile group:'de.sven-jacobs', name:'loremipsum', version:'1.0'
        compile group:'de.java2html',   name:'java2html',  version:'5.0'
    }

    vaadin {
        widgetset = 'fi.jasoft.dragdroplayouts.demo.DemoWidgetSet'
	serverPort 7676
	debug true

        devmode {
            superDevMode true
        }

	testbench {
		enabled true
	}

	plugin {
		logToConsole true
	}

    }

    war {
        // WAR filename
        archiveName = 'DragDropLayouts7.war'

        // Include widgetset
        dependsOn test, vaadinCompileWidgetset

        // Add sources
        webInf{
            into('classes'){
                from sourceSets.main.allJava
            }
        }
    }

   test {
	dependsOn vaadinCompileWidgetset
	systemProperties = System.properties
   }
}
